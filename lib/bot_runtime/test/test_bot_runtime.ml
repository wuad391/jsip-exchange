open! Core
open! Async
open Jsip_types
open Jsip_fundamental
open Jsip_bot_runtime

let aapl = Symbol_id.of_int 0
let alice = Participant.of_string "Alice"
let bob = Participant.of_string "Bob"

let oracle_config =
  Symbol_id.Map.of_alist_exn
    [ ( aapl
      , { Fundamental_oracle.Config.initial_price_cents = 15000
        ; volatility_cents_per_sec = 0.0
        ; mean_reversion_strength = 0.0
        ; tick_interval = Time_ns.Span.of_sec 1.0
        } )
    ]
;;

(* A bot that records every event passed to [on_event], so we can assert what
   the runtime forwards. *)
module Recording_bot = struct
  module Config = struct
    type t = { observed : Exchange_event.t list ref }
  end

  let name = "recording"
  let on_start _ _ctx = return ()
  let on_tick _ _ctx = return ()

  let on_event (cfg : Config.t) _ctx event =
    cfg.observed := event :: !(cfg.observed);
    return ()
  ;;
end

let make_recording_bot ~participant =
  let oracle = Fundamental_oracle.create oracle_config ~seed:1 in
  let submit _req = return (Ok ()) in
  let cancel _id = return (Ok ()) in
  let observed = ref [] in
  let bot =
    Bot_runtime.create
      (module Recording_bot)
      { observed }
      ~participant
      ~oracle
      ~rng:(Splittable_random.of_int 0)
      ~submit
      ~cancel
      ~tick_interval:(Time_ns.Span.of_sec 1.0)
  in
  bot, observed
;;

let bbo_event : Exchange_event.t =
  Best_bid_offer_update
    { symbol = aapl
    ; bbo =
        { bid =
            Some { price = Price.of_int_cents 14990; size = Size.of_int 100 }
        ; ask =
            Some { price = Price.of_int_cents 15010; size = Size.of_int 200 }
        }
    }
;;

let fill_event : Exchange_event.t =
  Fill
    { fill_id = 1
    ; symbol = aapl
    ; price = Price.of_int_cents 15000
    ; size = Size.of_int 50
    ; aggressor_order_id = Order_id.For_testing.of_int 1
    ; aggressor_client_order_id = Client_order_id.of_int 1
    ; aggressor_participant = alice
    ; aggressor_side = Buy
    ; resting_order_id = Order_id.For_testing.of_int 2
    ; resting_client_order_id = Client_order_id.of_int 1
    ; resting_participant = bob
    }
;;

let accepted_event : Exchange_event.t =
  Order_accept
    { order_id = Order_id.For_testing.of_int 1
    ; participant = alice
    ; request =
        { symbol = aapl
        ; participant = alice
        ; side = Buy
        ; price = Price.of_int_cents 15000
        ; size = Size.of_int 10
        ; time_in_force = Day
        ; client_order_id = Client_order_id.of_int 1
        }
    }
;;

let print_observed observed =
  let events = List.rev !observed in
  print_s [%sexp (events : Exchange_event.t list)]
;;

let%expect_test "feed_event forwards every event verbatim to on_event" =
  (* The runtime does not filter — the gateway's Dispatcher routes events to
     the right session pipe, so whatever the runtime receives is what the
     bot's [on_event] sees. *)
  let bot, observed = make_recording_bot ~participant:alice in
  let%bind () = Bot_runtime.feed_event bot bbo_event in
  let%bind () = Bot_runtime.feed_event bot fill_event in
  let%bind () = Bot_runtime.feed_event bot accepted_event in
  print_observed observed;
  [%expect
    {|
    ((Best_bid_offer_update (symbol 0)
      (bbo
       ((bid (((price 14990) (size 100)))) (ask (((price 15010) (size 200)))))))
     (Fill
      ((fill_id 1) (symbol 0) (price 15000) (size 50) (aggressor_order_id 1)
       (aggressor_client_order_id 1) (aggressor_participant Alice)
       (aggressor_side Buy) (resting_order_id 2) (resting_client_order_id 1)
       (resting_participant Bob)))
     (Order_accept (order_id 1) (participant Alice)
      (request
       ((symbol 0) (participant Alice) (side Buy) (price 15000) (size 10)
        (time_in_force Day) (client_order_id 1)))))
    |}];
  return ()
;;

let%expect_test "fundamental price is read from the oracle" =
  let bot, _observed = make_recording_bot ~participant:alice in
  let ctx = Bot_runtime.For_testing.context_of bot in
  print_s [%sexp (Bot_runtime.Context.fundamental ctx aapl : Price.t)];
  [%expect {| 15000 |}];
  return ()
;;

(* ----- stopping the tick loop ----- *)

(* A bot that counts its lifecycle calls, for the stop tests. *)
module Counting_bot = struct
  module Config = struct
    type t =
      { mutable starts : int
      ; mutable ticks : int
      }
  end

  let name = "counting"

  let on_start (cfg : Config.t) _ctx =
    cfg.starts <- cfg.starts + 1;
    return ()
  ;;

  let on_tick (cfg : Config.t) _ctx =
    cfg.ticks <- cfg.ticks + 1;
    return ()
  ;;

  let on_event _ _ctx _event = return ()
end

let make_counting_bot () =
  let oracle = Fundamental_oracle.create oracle_config ~seed:1 in
  let config : Counting_bot.Config.t = { starts = 0; ticks = 0 } in
  let bot =
    Bot_runtime.create
      (module Counting_bot)
      config
      ~participant:alice
      ~oracle
      ~rng:(Splittable_random.of_int 0)
      ~submit:(fun _req -> return (Ok ()))
      ~cancel:(fun _id -> return (Ok ()))
        (* Deliberately enormous: these tests must win the loop's race via
           [stop], never by an actual tick firing. *)
      ~tick_interval:(Time_ns.Span.of_day 1.0)
  in
  bot, config
;;

let%expect_test "stop before start: start returns at once, running nothing" =
  let bot, config = make_counting_bot () in
  Bot_runtime.stop bot;
  let%bind () = Bot_runtime.start bot in
  print_s [%message (config.starts : int) (config.ticks : int)];
  [%expect {| ((config.starts 0) (config.ticks 0)) |}];
  return ()
;;

let%expect_test "stop wakes a sleeping tick loop immediately" =
  let bot, config = make_counting_bot () in
  let started = Bot_runtime.start bot in
  (* Let [on_start] run and the loop settle into its (day-long) sleep. *)
  let%bind () = Scheduler.yield_until_no_jobs_remain () in
  print_s
    [%message (config.starts : int) (Deferred.is_determined started : bool)];
  [%expect
    {| ((config.starts 1) ("Deferred.is_determined started" false)) |}];
  Bot_runtime.stop bot;
  (* Idempotent: a second stop is a no-op. *)
  Bot_runtime.stop bot;
  let%bind () = started in
  print_s [%message (config.ticks : int)];
  [%expect {| (config.ticks 0) |}];
  return ()
;;

let%expect_test "bot_name is the strategy label, not the participant" =
  let bot, _config = make_counting_bot () in
  print_endline (Bot_runtime.bot_name bot);
  [%expect {| counting |}];
  return ()
;;
