open! Core
open Jsip_types
open Async_log_kernel.Ppx_log_syntax

type t =
  { symbol : Symbol.t
  ; mutable bids : Order.t list
  ; mutable asks : Order.t list
  }
[@@deriving sexp_of]

let create symbol = { symbol; bids = []; asks = [] }
let symbol t = t.symbol

(* grabs the list of orders associated with side *)
let side_list t side =
  match (side : Side.t) with Buy -> t.bids | Sell -> t.asks
;;

let set_side_list t side orders =
  match (side : Side.t) with
  | Buy -> t.bids <- orders
  | Sell -> t.asks <- orders
;;

let add t order =
  let side = Order.side order in
  set_side_list t side (order :: side_list t side)
;;

let remove' t order_id =
  let remove_from t side order_id =
    let orders = side_list t side in
    match
      List.partition_tf orders ~f:(fun o ->
        Order_id.equal (Order.order_id o) order_id)
    with
    | [], _ -> None
    | [ found ], rest ->
      set_side_list t side rest;
      Some found
    | matches, _ ->
      [%log.info
        "BUG: More than one order matching order_id found when removing"
          (order_id : Order_id.t)
          (matches : Order.t list)
          (t.symbol : Symbol.t)
          (side : Side.t)];
      None
  in
  match remove_from t Buy order_id with
  | Some _ as result -> result
  | None -> remove_from t Sell order_id
;;

let remove t order_id = ignore (remove' t order_id : Order.t option)

let find t order_id =
  let find_in side =
    List.find (side_list t side) ~f:(fun o ->
      Order_id.equal (Order.order_id o) order_id)
  in
  match find_in Buy with Some _ as result -> result | None -> find_in Sell
;;

let price_time_compare side o1 o2 =
  match
    ( Price.is_more_aggressive
        side
        ~price:(Order.price o1)
        ~than:(Order.price o2)
    , Price.is_more_aggressive
        side
        ~price:(Order.price o2)
        ~than:(Order.price o1)
    , Time_in_force.compare (Order.time_in_force o2) (Order.time_in_force o1)
    )
  with
  | true, _, _ -> 1
  | _, true, _ -> -1
  | _, _, x -> x
;;

(* NOTE: This walks the list front-to-back and returns the *first* tradable
   order, not the best-priced one. Orders are in reverse insertion order
   (newest first), so this matches against whatever was most recently added,
   regardless of price. See test_matching_engine.ml for a test that
   demonstrates why this is wrong. *)
(* Now this has been updated to return the most aggressively priced
   marketable order on the opposite side using List.reduce *)
let find_match t incoming =
  let incoming_side = Order.side incoming in
  let opposite_side = Side.flip incoming_side in
  let resting_orders =
    List.filter (side_list t opposite_side) ~f:(fun order ->
      Price.is_marketable
        incoming_side
        ~price:(Order.price incoming)
        ~resting_price:(Order.price order))
  in
  List.reduce resting_orders ~f:(fun r1 r2 ->
    match price_time_compare opposite_side r1 r2 with
    | 1 -> r1
    | -1 -> r2
    | 0 -> r1
    | _ -> raise_s [%message "invalid"])
;;

let orders_on_side t side = side_list t side
let is_empty t = List.is_empty t.bids && List.is_empty t.asks
let count t side = List.length (side_list t side)

(* gets the best price on the side *)
let best_price t side =
  let price_list =
    List.map (side_list t side) ~f:(fun order -> Order.price order)
  in
  List.reduce price_list ~f:(fun a b ->
    if Price.is_more_aggressive side ~price:a ~than:b then a else b)
;;

(* match side_list t side with | [] -> None | first :: rest -> let is_better
   = match (side : Side.t) with Buy -> Price.( > ) | Sell -> Price.( < ) in
   Some (List.fold rest ~init:(Order.price first) ~f:(fun best order -> let
   price = Order.price order in if is_better price best then price else
   best)) *)

let best_level t side : Level.t option =
  match best_price t side with
  | None -> None
  | Some price ->
    let total_size =
      List.fold (side_list t side) ~init:Size.zero ~f:(fun acc order ->
        if Price.equal (Order.price order) price
        then Size.( + ) acc (Order.remaining_size order)
        else acc)
    in
    Some { price; size = total_size }
;;

let best_bid_offer t : Bbo.t =
  { bid = best_level t Buy; ask = best_level t Sell }
;;

(* TODO: This function seems kind of ugly. We should check if there is a
   better way to do this, esp if there is more elegant way to do the compare.
   Also check out the hints?? I think there is something we are missing in
   the hints. *)
let snapshot_side t (side : Side.t) =
  orders_on_side t side
  |> List.sort ~compare:(price_time_compare side)
  |> List.map ~f:Level.of_order
;;

let snapshot t =
  { Book.symbol = symbol t
  ; bids = snapshot_side t Buy
  ; asks = snapshot_side t Sell
  ; bbo = best_bid_offer t
  }
;;

module For_testing = struct
  let remove = remove'
end
